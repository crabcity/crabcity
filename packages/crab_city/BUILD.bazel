load("@aspect_bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_test")

package(default_visibility = ["//visibility:public"])

# Base crab binary without embedded UI
rust_binary(
    name = "crab",
    srcs = glob(
        ["src/**/*.rs"],
        exclude = ["src/embedded_ui.rs"],
    ),
    compile_data = ["src/terminal.js"],
    edition = "2024",
    deps = [
        "//packages/claude_convo",
        "//packages/compositor",
        "//packages/crab_city_auth",
        "//packages/pty_manager",
        "//packages/virtual_terminal",
        "@crate_index//:anyhow",
        "@crate_index//:argon2",
        "@crate_index//:axum",
        "@crate_index//:chrono",
        "@crate_index//:clap",
        "@crate_index//:dirs",
        "@crate_index//:figment",
        "@crate_index//:futures",
        "@crate_index//:ignore",
        "@crate_index//:iroh",
        "@crate_index//:iroh-relay",
        "@crate_index//:maud",
        "@crate_index//:nix",
        "@crate_index//:password-hash",
        "@crate_index//:rand",
        "@crate_index//:ratatui",
        "@crate_index//:regex",
        "@crate_index//:reqwest",
        "@crate_index//:rpassword",
        "@crate_index//:serde",
        "@crate_index//:serde_json",
        "@crate_index//:similar",
        "@crate_index//:sqlx",
        "@crate_index//:syndiff",
        "@crate_index//:thiserror",
        "@crate_index//:tokio",
        "@crate_index//:tokio-tungstenite",
        "@crate_index//:tokio-util",
        "@crate_index//:toml",
        "@crate_index//:tower",
        "@crate_index//:tower-http",
        "@crate_index//:tracing",
        "@crate_index//:tracing-subscriber",
        "@crate_index//:tree-sitter",
        "@crate_index//:tree-sitter-bash",
        "@crate_index//:tree-sitter-css",
        "@crate_index//:tree-sitter-go",
        "@crate_index//:tree-sitter-html",
        "@crate_index//:tree-sitter-javascript",
        "@crate_index//:tree-sitter-json",
        "@crate_index//:tree-sitter-language",
        "@crate_index//:tree-sitter-python",
        "@crate_index//:tree-sitter-rust",
        "@crate_index//:tree-sitter-toml-ng",
        "@crate_index//:tree-sitter-typescript",
        "@crate_index//:uuid",
        "@crate_index//:vt100",
        "@crate_index//:walkdir",
    ],
)

# crab with embedded SvelteKit UI - single binary deployment
rust_binary(
    name = "crab_embedded",
    srcs = glob(["src/**/*.rs"]),
    compile_data = ["src/terminal.js"],
    crate_features = ["embedded-ui"],
    edition = "2024",
    deps = [
        "//packages/claude_convo",
        "//packages/compositor",
        "//packages/crab_city_auth",
        "//packages/crab_city_ui/crate:crab_city_ui_crate",
        "//packages/pty_manager",
        "//packages/virtual_terminal",
        "@crate_index//:anyhow",
        "@crate_index//:argon2",
        "@crate_index//:axum",
        "@crate_index//:chrono",
        "@crate_index//:clap",
        "@crate_index//:dirs",
        "@crate_index//:figment",
        "@crate_index//:futures",
        "@crate_index//:ignore",
        "@crate_index//:iroh",
        "@crate_index//:iroh-relay",
        "@crate_index//:maud",
        "@crate_index//:mime_guess",
        "@crate_index//:nix",
        "@crate_index//:password-hash",
        "@crate_index//:rand",
        "@crate_index//:ratatui",
        "@crate_index//:regex",
        "@crate_index//:reqwest",
        "@crate_index//:rpassword",
        "@crate_index//:rust-embed",
        "@crate_index//:serde",
        "@crate_index//:serde_json",
        "@crate_index//:similar",
        "@crate_index//:sqlx",
        "@crate_index//:syndiff",
        "@crate_index//:thiserror",
        "@crate_index//:tokio",
        "@crate_index//:tokio-tungstenite",
        "@crate_index//:tokio-util",
        "@crate_index//:toml",
        "@crate_index//:tower",
        "@crate_index//:tower-http",
        "@crate_index//:tracing",
        "@crate_index//:tracing-subscriber",
        "@crate_index//:tree-sitter",
        "@crate_index//:tree-sitter-bash",
        "@crate_index//:tree-sitter-css",
        "@crate_index//:tree-sitter-go",
        "@crate_index//:tree-sitter-html",
        "@crate_index//:tree-sitter-javascript",
        "@crate_index//:tree-sitter-json",
        "@crate_index//:tree-sitter-language",
        "@crate_index//:tree-sitter-python",
        "@crate_index//:tree-sitter-rust",
        "@crate_index//:tree-sitter-toml-ng",
        "@crate_index//:tree-sitter-typescript",
        "@crate_index//:uuid",
        "@crate_index//:vt100",
        "@crate_index//:walkdir",
    ],
)

# Test targets split by module for parallel execution and caching.
# Each target filters to a module prefix via TESTBRIDGE_TEST_ONLY.
_TEST_DEPS = ["@crate_index//:tempfile"]

_TEST_FILTERS = {
    "models_test": "models::tests",
    "ws_test": "ws::",
    "git_test": "git::",
    "handlers_test": "handlers::",
    "repository_test": "repository::",
    "inference_test": "inference::",
    "cli_test": "cli::",
    "files_test": "files::",
}

[rust_test(
    name = name,
    crate = ":crab",
    env = {"TESTBRIDGE_TEST_ONLY": test_filter},
    deps = _TEST_DEPS,
) for name, test_filter in _TEST_FILTERS.items()]

# Remaining tests not covered by filtered targets above
rust_test(
    name = "misc_test",
    args = [arg for test_filter in _TEST_FILTERS.values() for arg in [
        "--skip",
        test_filter,
    ]],
    crate = ":crab",
    deps = _TEST_DEPS,
)

test_suite(
    name = "crab_city_tests",
    tests = [":" + name for name in _TEST_FILTERS.keys()] + [":misc_test"],
)
